#version 430 core

layout (local_size_x = 16, local_size_y = 16) in;

layout (binding = 0, rgba32f) uniform image2D normalMap;

layout (binding = 0) uniform sampler2D heightMap;
uniform int N;
uniform float strength;

void main(void) {
    vec2 uv = gl_GlobalInvocationID.xy / float(N);
    float x = 1.0 / N;

    float z0 = texture(heightMap, uv + vec2(-x,-x)).r;
    float z1 = texture(heightMap, uv + vec2(0,-x)).r;
    float z2 = texture(heightMap, uv + vec2(x,-x)).r;
    float z3 = texture(heightMap, uv + vec2(-x,0)).r;
    float z4 = texture(heightMap, uv + vec2(x,0)).r;
    float z5 = texture(heightMap, uv + vec2(-x,x)).r;
    float z6 = texture(heightMap, uv + vec2(0,x)).r;
    float z7 = texture(heightMap, uv + vec2(x,x)).r;

    vec3 normal = vec3(
        z0 + 2.0*z3 + z5 - z2 - 2.0*z4 - z7,
        1.0 / strength,
        z0 + 2.0*z1 + z2 -z5 - 2.0*z6 - z7
    );
    imageStore(normalMap, ivec2(gl_GlobalInvocationID.xy), vec4(normalize(normal), 1.0));
}
